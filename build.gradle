apply plugin: 'idea'

// Exclude apps/ dir itself, but include its subdirs
def javaProjects = subprojects - project(':apps')

configure(javaProjects) {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    repositories {
        jcenter()
    }

    dependencies {
        testCompile 'junit:junit:4.+'
        testCompile 'org.hamcrest:hamcrest-library:1.3'
        testCompile 'org.mockito:mockito-core:2.11.0'
    }

    jar {
        manifest {
            attributes 'Implementation-Version': '0.9.31-7'
        }
    }

    sourceCompatibility = 1.7
    // Set i2pBootClasspath=/path/to/rt.jar:/path/to/jce.jar in ~/.gradle/gradle.properties if needed
    if (i2pBootClasspath) {
        tasks.withType(AbstractCompile, { AbstractCompile ac ->
            ac.options.bootstrapClasspath = files(i2pBootClasspath)
        })
    }
}

task codeCoverageReport(type: JacocoReport) {
    dependsOn(javaProjects.test)

    jacocoClasspath = project(':core').configurations.jacocoAnt
    additionalSourceDirs = files(javaProjects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(javaProjects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(javaProjects.sourceSets.main.output)
    executionData = files(javaProjects.jacocoTestReport.executionData)

    doFirst {
        executionData = files(executionData.findAll { it.exists() })
    }

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled true
        html.destination "${buildDir}/reports/jacoco/html"
    }
}

//apply from: file('gradle/update.gradle')
